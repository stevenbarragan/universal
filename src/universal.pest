language = _{ NEWLINE* ~ (module | expression | function_def) }

expression = { assignation | import | (boolean ~ (boolean_operator ~ boolean)*) }
boolean_operator = { "==" | "!=" | "<=" | ">=" | "<" | ">" | "min" | "max" }
boolean = { summand ~ (summand_operator ~ summand)* }
summand_operator = { "+" | "-" }
summand = { unary ~ (factor_operator ~ unary)*  }
factor_operator = { "*" | "/" }
unary = { (unary_operator ~ primary) | primary }
unary_operator = { "!" | "-" }
primary = { "(" ~ expression ~ ")" | endvalues }
endvalues  = _{ symbol | float | integer | conditional | function_call | bool | variable }

float = @{ integer ~ "." ~ integer }

bool = { thruty | falsey }
thruty = { "true" }
falsey = { "false" }

assignation = { (variable_def | variable) ~ "=" ~ expression }

import = { "import" ~ filepath }
filepath = @{ LETTER+ }

conditional = { conditional_type ~ expression ~ block ~ else_block? ~ "end" }
conditional_type = { "if" | "unless" }
else_block = _{ "else" ~ block }

module = { "module" ~ name ~ module_block ~ "end" }
module_block = { (NEWLINE* ~ (expression | function_def))+ ~ NEWLINE* }

integer = @{ NUMBER+ }

function_call = { function_name ~ "(" ~ function_params? ~ ")" }
function_params = { expression ~ ("," ~ expression)* }
function_name = { name ~ ("." ~ name)? }

function_def = { public? ~ "fn" ~ name ~ ("(" ~ params? ~ ")")? ~ ":" ~ results ~ block ~ "end" }
public = { "export" }

block = { (NEWLINE* ~ expression)+ ~ NEWLINE* }

results = { kind ~ ("," ~ kind)* }
params = { param ~ ("," ~ param)* }
param = { variable ~ ":" ~ kind }

variable_def = { variable ~ ":" ~ kind }
variable = { !reserve_word ~ name }
kind = { name }

name = @{ LETTER ~ (LETTER | NUMBER)* }

symbol = { ":" ~ (symbol_value | "\"" ~ string_value ~ "\"") }
symbol_value = @{ (LETTER | NUMBER)+ }
string_value = { (LETTER | NUMBER)* }

reserve_word = _{ "end" | "fn" | "module" | "if" | "else" | "unless" | "export" }

WHITESPACE = _{ " " | "\t" | "\r" }
NEWLINE = _{ "\n" | ";" }
